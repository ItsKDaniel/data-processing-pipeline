plugins {
    id 'org.springframework.boot' version "${springVersion}"
    id 'io.spring.dependency-management' version "${springDependencyManagementVersion}"
    id 'java'
    id 'jacoco'
    id 'org.sonarqube' version "${sonarVersion}"
}

group = 'io.falcon'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // core app dependencies
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-websocket'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // websocket ui dependencies
    implementation 'org.webjars:webjars-locator-core'
    implementation 'org.webjars:sockjs-client:1.0.2'
    implementation 'org.webjars:stomp-websocket:2.3.3'
    implementation 'org.webjars:bootstrap:4.6.0-1'
    implementation 'org.webjars:jquery:3.6.0'

    // documentation dependencies
    implementation 'org.springdoc:springdoc-openapi-ui:1.5.6'

    // lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // test dependencies
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

test {
    useJUnitPlatform()
    jacoco {
        enabled = true
        destinationFile = file("${buildDir}/jacoco/jacocoTest.exec")
        classDumpDir = file("${buildDir}/jacoco/classpathdumps")
    }
}

jacoco {
    toolVersion = '0.8.6'
}

jacocoTestReport {
    reports {
        xml.enabled true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
        csv.enabled false
        html.enabled false
    }
}

compileJava {
    options.encoding = 'UTF-8'
}

bootJar {
    enabled = true
    archiveFileName = "falcon-data-pipeline.jar"
    manifest {
        attributes 'provider': 'gradle'
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    published
    [apiElements, runtimeElements].each {
        it.outgoing.artifacts.removeIf { it.buildDependencies.getDependencies(null).contains(jar) }
        it.outgoing.artifact(bootJar)
    }
}

sonarqube {
    properties {
        property 'sonar.scm.provider', 'git'
        property 'sonar.projectName', "${project.name}"
        property 'sonar.projectKey', "${project.name}"
        property 'sonar.sources', "src/main"
        property 'sonar.tests', "src/test"
        property 'sonar.ws.timeout', '600'
        property 'sonar.coverage.exclusions', '**/model/**,**/exceptions/**,**/config/**,**/static/**'
        property 'sonar.coverage.jacoco.xmlReportPaths', "${buildDir}/reports/jacoco/report.xml"
        property "sonar.junit.reportsPaths", "${buildDir}/test-results/"
        property 'sonar.host.url', "${sonarurl}"
        property 'sonar.login', "${sonarlogin}"
    }
}

tasks.jacocoTestReport.dependsOn(compileTestJava)
tasks.sonarqube.dependsOn(jacocoTestReport)
